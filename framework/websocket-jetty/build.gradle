plugins {
	id "java"
	id "org.springframework.boot"
	id "org.springframework.aot.smoke-test"
	id "org.graalvm.buildtools.native"
}

configurations {
	all {
		resolutionStrategy {
			eachDependency { dependency ->
				if (dependency.requested.group == "jakarta.servlet" && dependency.requested.name == "jakarta.servlet-api") {
					// Downgrade for Jetty, see https://github.com/spring-projects/spring-boot/issues/33044
					dependency.useVersion("5.0.0")
				}
			}
		}
	}
}

dependencies {
	implementation(enforcedPlatform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES))
	implementation("org.springframework.boot:spring-boot-starter-websocket")
	implementation("org.springframework.boot:spring-boot-starter-jetty")
	modules {
		module("org.springframework.boot:spring-boot-starter-tomcat") {
			replacedBy("org.springframework.boot:spring-boot-starter-jetty", "Use Jetty instead of Tomcat")
		}
	}
	testImplementation("org.springframework.boot:spring-boot-starter-test")

	appTestImplementation(project(":aot-smoke-test-support"))
	appTestImplementation("org.awaitility:awaitility:4.2.0")
}

aotSmokeTest {
	webApplication = true
	nativeAppTest {
    		expectedToFail {
    			becauseOf "https://github.com/jetty/jetty.project/issues/11683"
    		}
    	}
}
