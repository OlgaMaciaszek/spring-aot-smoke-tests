pluginManagement {
	includeBuild "gradle/plugins/aot-smoke-test-plugin"
	repositories {
		maven {
			url "https://repo.spring.vmware.com/artifactory/spring-commercial-snapshot-local"
			credentials {
				username System.env['REPO_SPRING_VMWARE_COM_USERNAME']
				password System.env['REPO_SPRING_VMWARE_COM_PASSWORD']
			}
		}
		maven {
			url "https://repo.spring.vmware.com/artifactory/spring-commercial-release-local"
			credentials {
				username System.env['REPO_SPRING_VMWARE_COM_USERNAME']
				password System.env['REPO_SPRING_VMWARE_COM_PASSWORD']
			}
		}
		gradlePluginPortal()
		maven {
			url "https://repo.spring.io/snapshot"
		}
		maven {
			url 'https://repo.spring.io/milestone'
		}
		maven {
			url "https://repo.spring.io/release"
			content {
				includeGroup "io.spring.ge.conventions"
			}
		}
	}
}

plugins {
	id "com.gradle.enterprise" version "3.15.1"
	id "io.spring.ge.conventions" version "0.0.15"
}

rootProject.name="spring-aot-smoke-tests"

include "aot-smoke-test-support"
include "aot-smoke-test-third-party-hints"

["batch", "boot", "cloud", "data", "framework", "graphql", "integration", "security", "session"].each { group ->
	file(group).eachDirMatch(~/[a-z].*/) { smokeTest ->
		include "$group:${smokeTest.name}"
	}
}

gradleEnterprise {
	buildScan {
		value("Spring Boot generation", "${springBootGeneration}")
	}
}